group = "ec.com.levelap"
version = "0.0.1"

buildscript {
	ext {
		springBootVersion = '1.5.3.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
	baseName = 'Levelap'
	version = '0.0.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

configurations {
	providedRuntime
	jaxb
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web') {
		exclude module: 'spring-boot-starter-tomcat'
		exclude module: 'hibernate-validator'
	}
	compile('javax.servlet:javax.servlet-api:3.1.0')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile("org.springframework.boot:spring-boot-starter-mail")
	compile('commons-io:commons-io:1.3.2')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.hibernate:hibernate-core')
	compile('org.postgresql:postgresql')
	compile("org.springframework.ws:spring-ws-core")
	jaxb "com.sun.xml.bind:jaxb-xjc:2.1.7"
	
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

ext.wsldMethod = { type ->
	ext.sourcesDir = "src/main/java/ec/com/levelap/tcc/entity/" + type
	ext.schema = "http://clientes.tcc.com.co/servicios/" + type + ".asmx?WSDL"
	
	project.ant {
		taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath
		delete(dir: sourcesDir)
		mkdir(dir: sourcesDir)
		
		xjc(destdir: sourcesDir, schema: schema, package: "ec.com.levelap.tcc.entity." + type) {
			arg(value: "-wsdl")
			produces(dir: sourcesDir, includes: "**/*.java")
		}
		
		move(file: sourcesDir + "/ec/com/levelap/tcc/entity/" + type, tofile: sourcesDir)
	}
}

project.wsldMethod("wsrecogidas")
project.wsldMethod("wsDespachos")